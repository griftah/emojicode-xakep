📦 files 🔴

👴 аналог chr в питоне
🐋 🚂 🍇
    🐖 🎩 ➡ 🔣 🍇
        🍎 🍺 🔬 🔤?????????❌t❌n??❌r?????????????????? !"👴$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_`abcdefghijklmnopqrstuvwxyz{|}~🔤 🐕
    🍉
🍉


🐇 📽 🍇
    🍰 code 🍨🐚🔣
    🍰 args 🍨🐚🚂

    🐈 📜 @text 🔡 @optimize 👌 🍇
        🍮 code 🍨 🔟b 🍆
        🍮 args 🍨 1 🍆
        🍦 brackets 🔷 🍨🐚🚂 🐸

        🍮 pc 0
        🔂 char @text 🍇
            🍊 🍦 legal 🔍 🔤<>+-[],.🔤 🔡 char 🍇
                🍊 🎊 🎊 @optimize ◀ legal 4 😛 char 🍺 🐽 code -1 🍇
                    🐷 args pc ➕ 🍺 🐽 args -1 1
                🍉
                🍓 🍇
                    🐻 code char
                    🐻 args 1
                    🍫 pc
                    🍊 😛 char 🔟[ 🍇
                        🐻 brackets pc
                    🍉
                    🍋 😛 char 🔟] 🍇
                        🍦 left 🍺 🐼 brackets
                        🍦 right pc
                        🐷 args left right
                        🐷 args right ➖ left 1
                    🍉
                🍉
            🍉
        🍉
    🍉

    🐖 🏃 🍇
        🍰 char 🔣
        🍦 stdout 🍩📤📄
        🍦 code_len 🐔 code
        🍦 tape 🔷 🍨🐚🚂 🐧 32000
        🍮 xc 0
        🍮 pc 0

        🔂 индекс ⏩ 0 32000 🍇
            🐻 tape 0
        🍉

        🔁 ◀ pc code_len 🍇
            🍮 char 🍺 🐽 code pc
            🍮 arg 🍺 🐽 args pc
            🍊 😛 char 🔟+ 🍇
                🐷 tape xc ➕ 🍺 🐽 tape xc arg
            🍉
            🍋 😛 char 🔟- 🍇
                🐷 tape xc ➖ 🍺 🐽 tape xc arg
            🍉
            🍋 😛 char 🔟> 🍇
                🍮 xc ➕ xc arg
            🍉
            🍋 😛 char 🔟< 🍇
                🍮 xc ➖ xc arg
            🍉
            🍋 🎊 😛 char 🔟[  😛 🍺 🐽 tape xc 0 🍇
                🍮 pc arg
            🍉
            🍋 😛 char 🔟]  🍇
                🍮 pc arg
            🍉
            🍋 😛 char 🔟. 🍇
                ✏ stdout 📇 🔡 🎩 🍺 🐽 tape xc
            🍉
            🍫 pc
        🍉
    🍉
🍉


🏁 🍇
    🍮 исходник 🔤++++++++++[>+++++++>++++++++++>+++>+<<<<-]>++.>+.+++++++..+++.>++.<<+++++++++++++++.>.+++.------.--------.>+.>.🔤
    🍮 оптимизировать 👎

    🍦 аргументы 🍩🎞💻
    🔂 индекс ⏩ 2 🐔 аргументы 🍇
        🍦 аргумент 🍺 🐽 аргументы индекс
        🍊 😛 аргумент 🔤-o🔤 🍇
            🍮 оптимизировать 👍
        🍉
        🍓 🍇
            🍮 исходник 🍺 🔡 🍺 🍩 📇 📄 аргумент
        🍉
    🍉

    🍦 program 🔷 📽📜 исходник оптимизировать
    🏃 program
🍉
