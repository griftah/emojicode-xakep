👴 основано на интерпретаторе брейнфака на си, взятом с
👴 http://esoteric.sange.fi/brainfuck/impl/interp/BFI.c

📦 files 🔴

👴 аналог chr в питоне
🐋 🚂 🍇
    🐖 🎩 ➡ 🔣 🍇
        🍎 🍺 🔬 🔤?????????❌t❌n??❌r?????????????????? !"👴$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_`abcdefghijklmnopqrstuvwxyz{|}~🔤 🐕
    🍉
🍉

🏁 🍇
    🍦 stdout 🍩📤📄
    👴 🍦 stdin 🍩📥📄

    🍦 аргументы 🍩🎞💻
    🍮 code 🔤🔤
    🍊 🍦 имя-файла 🐽 аргументы 2 🍇
        🍮 code 🍺 🔡 🍺 🍩 📇 📄 имя-файла
    🍉
    🍓 🍇
        🍮 code 🔤++++++++[>+++++++++<-]>.<+++++[>++++++<-]>-.+++++++..+++.<
++++++++[>>++++<<-]>>.<<++++[>------<-]>.<++++[>++++++<-]>
.+++.------.--------.>+.🔤
    🍉

    🍦 prog_len 📏 code
    🍦 tape 🍨 0 🍆

    🍮 xc 0
    🍮 pc 0
    🍮 l 0

    🍰 char 🔣
    🔁 ◀ pc prog_len 🍇
        🍮 char 🍺 🔬 code pc
        🍊 😛 char 🔟+ 🍇
            🐷 tape xc ➕ 🍺 🐽 tape xc 1
        🍉
        🍋 😛 char 🔟- 🍇
            🐷 tape xc ➖ 🍺 🐽 tape xc 1
        🍉
        🍋 😛 char 🔟. 🍇
            ✏ stdout 📇 🔡 🎩 🍺 🐽 tape xc
        🍉
        🍋 😛 char 🔟> 🍇
            🍫 xc 👴 xc++
            🍊 ⬅ 🐔 tape xc 🍇
                🐻 tape 0
            🍉
        🍉
        🍋 😛 char 🔟< 🍇
            🍊 ▶ xc 0 🍇
                🍳 xc
            🍉
        🍉
        🍋 🎊 😛 char 🔟[ 😛 🍺 🐽 tape xc 0 🍇
            🍫 pc
            🍮 char 🍺 🔬 code pc
            🔁 🎉 ▶ l 0 ❎ 😛 char 🔟] 🍇
                🍊 😛 char 🔟[ 🍇
                    🍫 l
                🍉
                🍋 😛 char 🔟] 🍇
                    🍳 l
                🍉
                🍫 pc
                🍮 char 🍺 🔬 code pc
            🍉
        🍉
        🍋 😛 char 🔟] 🍇
            🍳 pc
            🍮 char 🍺 🔬 code pc
            🔁 🎉 ▶ l 0 ❎ 😛 char 🔟[ 🍇
                🍊 😛 char 🔟] 🍇
                    🍫 l
                🍉
                🍋 😛 char 🔟[ 🍇
                    🍳 l
                🍉
                🍳 pc
                🍮 char 🍺 🔬 code pc
            🍉
            🍳 pc
        🍉
        🍫 pc
    🍉
🍉
