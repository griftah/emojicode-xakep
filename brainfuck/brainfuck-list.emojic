👴 основано на питоновском брейнфаке, взятом из
👴 https://github.com/kostya/benchmarks/tree/master/brainfuck

📦 files 🔴

👴 аналог chr в питоне
🐋 🚂 🍇
    🐖 🎩 ➡ 🔣 🍇
        🍎 🍺 🔬 🔤?????????❌t❌n??❌r?????????????????? !"👴$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_`abcdefghijklmnopqrstuvwxyz{|}~🔤 🐕
    🍉
🍉


👴 Tape
🐇 🎞 🍇
    🍰 tape 🍨🐚🚂
    🍰 pos 🚂

    🐈 💡 🍇
        🍮 tape 🍨 0 🍆
        🔂 i ⏩ 0 10000 🍇
            🐻 tape 0
        🍉
        🍮 pos 0
    🍉

    👴 get
    🐖 🐽 ➡ 🚂 🍇
        🍎 🍺 🐽 tape pos
    🍉

    👴 inc
    🐖 👧 🍇
        👴 self.tape[self.pos] += 1
        🐷 tape pos ➕ 🐽 🐕 1
    🍉

    👴 dec
    🐖 👦 🍇
        👴 self.tape[self.pos] -= 1
        🐷 tape pos ➖ 🐽 🐕 1
    🍉

    👴 advance
    🐖 👉 🍇
        🍫 pos
        🍊 ⬅ 🐔 tape pos 🍇
            🐻 tape 0
        🍉
    🍉

    👴 devance
    🐖 👈 🍇
        🍊 ▶ pos 0 🍇
            🍳 pos
        🍉
    🍉
🍉


🐇 📽 🍇
    🍰 code 🔡
    🍰 bracket_map 🍨🐚🚂

    🐈 📜 @text 🔡 🍇
        🍮 code 🔤🔤
        🍮 bracket_map 🔷 🍨🐚🚂 🐸
        🍦 leftstack 🔷 🍨🐚🚂 🐸

        🍮 pc 0
        🔂 char @text 🍇
            🐻 bracket_map 0
            🍊 🍦 legal 🔍 🔤[]<>+-,.🔤 🔡 char 🍇
                🍮 code 📝 code char
                🍊 😛 char 🔟[ 🍇
                    🐻 leftstack pc
                🍉
                🍋 🎊 😛 char 🔟]   ▶ 🐔 leftstack 0 🍇
                    🍦 left 🍺 🐼 leftstack
                    🍦 right pc
                    🐷 bracket_map left right
                    🐷 bracket_map right left
                🍉
                🍫 pc
            🍉
        🍉
    🍉

    🐖 🏃 🍇
        🍮 pc 0
        🍦 tape 🔷 🎞💡
        🍦 stdout 🍩📤📄

        🔁 ◀ pc 📏 code 🍇
            🍦 char 🍺 🔬 code pc

            🍊 😛 char 🔟+ 🍇
                👧 tape
            🍉
            🍋 😛 char 🔟- 🍇
                👦 tape
            🍉
            🍋 😛 char 🔟> 🍇
                👉 tape
            🍉
            🍋 😛 char 🔟< 🍇
                👈 tape
            🍉
            🍋 🎊 😛 char 🔟[  😛 🐽 tape 0 🍇
                🍮 pc 🍺 🐽 bracket_map pc
            🍉
            🍋 🎊 😛 char 🔟]  ❎ 😛 🐽 tape 0 🍇
                🍮 pc 🍺 🐽 bracket_map pc
                👴 🍳 pc
            🍉
            🍋 😛 char 🔟. 🍇
                ✏ stdout  📇 🔡 🎩 🐽 tape
            🍉
            🍫 pc
        🍉
    🍉
🍉


🏁 🍇
    🍦 аргументы 🍩🎞💻
    🍮 исходник 🔤🔤
    🍊 🍦 имя-файла 🐽 аргументы 2 🍇
        🍮 исходник 🍺 🔡 🍺 🍩 📇 📄 имя-файла
    🍉
    🍓 🍇
        🍮 исходник 🔤++++++++[>+++++++++<-]>.<+++++[>++++++<-]>-.+++++++..+++.<
++++++++[>>++++<<-]>>.<<++++[>------<-]>.<++++[>++++++<-]>
.+++.------.--------.>+.🔤
    🍉
    🍦 program 🔷 📽📜 исходник
    🏃 program
🍉
