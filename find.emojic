📜 🔤codes.emojic🔤

🐇 🚲 🍇
    🐊 🍡🐚🌻

    🍰 словарь 🍨🐚🌻
    🍰 слово 🔡
    🍰 индекс 🚂

    🐈 🔔 @словарь 🍨🐚🌻 @слово 🔡 🍇
        🍮 словарь @словарь
        🍮 слово @слово
        🍮 индекс 0
        🏃 🐕
    🍉

    👴 промотать индекс до ближайшего
    👴 эмодзи с нужным словом в названии
    🐖 🏃 🍇
        🍦 стоп 🐔 словарь
        🔁 ◀ индекс стоп 🍇
            🍊 🍦 эмодзи 🐽 словарь индекс 🍇
                🍊 ❎ ☁ 🔍 🏷 эмодзи слово 🍇
                    🍎 ⚡
                🍉
            🍉
            🍫 индекс
        🍉
    🍉

    👴 вернуть следующий элемент
    🐖 🔽 ➡ 🌻 🍇
        🍦 эмодзи 🍺 🐽 словарь индекс
        🍫 индекс
        🏃 🐕
        🍎 эмодзи
    🍉

    👴 есть ли ещё элементы
    🐖 ❓ ➡ 👌 🍇
        🍎 ◀ индекс 🐔 словарь
    🍉
🍉


🐇 🚴 🍇
    🐊 🔂🐚🌻

    🍰 словарь 🍨🐚🌻
    🍰 слово 🔡

    🐈 🔔 @словарь 🍨🐚🌻 @слово 🔡 🍇
        🍮 словарь @словарь
        🍮 слово @слово
    🍉

    🐖 🍡 ➡ 🚲 🍇
        🍎 🔷 🚲🔔 словарь слово
    🍉
🍉


🐋 📖 🍇
    🐖 🔪 @слово 🔡 ➡ 🚴 🍇
        🍎 🔷 🚴🔔 словарь @слово
    🍉
🍉


🏁 🍇
    🍦 аргументы 🍩🎞💻
    🍊 🍦 слово 🐽 аргументы 2 🍇
        🍦 словарь-эмодзи 🔷 📖📖 🔤emoji.txt🔤
        🍮 количество 0
        🔂 эмодзи 🔪 словарь-эмодзи слово 🍇
            😀 эмодзи
            🍫 количество
        🍉
        🍊 😛 количество 0 🍇
            😀 🍪🔤Эмодзи со словом "🔤 слово 🔤" в названиии нет🔤🍪
        🍉
    🍉
    🍓 🍇
        😀 🔤Список эмодзи с нужным словом в описании🔤
        😀 🔤Запуск: emojicode find.emojib слово🔤
    🍉
🍉
