📜 🔤codes.emojic🔤

🐇 🚲 🍇
    🐊 🍡🐚🌻

    🍰 список 🍨🐚🌻
    🍰 слово 🔡
    🍰 индекс 🚂

    🐈 🔔 @список 🍨🐚🌻 @слово 🔡 🍇
        🍮 список @список
        🍮 слово @слово
        🍮 индекс 0
        🏃 🐕
    🍉

    👴 промотать индекс до ближайшего
    👴 эмодзи с нужным словом в названии
    🐖 🏃 🍇
        🍦 стоп 🐔 список
        🔁 ◀ индекс стоп 🍇
            🍊 🍦 эмодзи 🐽 список индекс 🍇
                🍊 ❎ ☁ 🔍 🏷 эмодзи слово 🍇
                    🍎 ⚡
                🍉
            🍉
            🍫 индекс
        🍉
    🍉

    👴 вернуть следующий элемент
    🐖 🔽 ➡ 🌻 🍇
        🍦 эмодзи 🍺 🐽 список индекс
        🍫 индекс
        🏃 🐕
        🍎 эмодзи
    🍉

    👴 есть ли ещё элементы
    🐖 ❓ ➡ 👌 🍇
        🍎 ◀ индекс 🐔 список
    🍉
🍉


🐇 🚴 🍇
    🐊 🔂🐚🌻

    🍰 список 🍨🐚🌻
    🍰 слово 🔡

    🐈 🔔 @список 🍨🐚🌻 @слово 🔡 🍇
        🍮 список @список
        🍮 слово @слово
    🍉

    🐖 🍡 ➡ 🚲 🍇
        🍎 🔷 🚲🔔 список слово
    🍉
🍉


🐋 📚 🍇
    🐖 🔪 @слово 🔡 ➡ 🚴 🍇
        🍎 🔷 🚴🔔 список @слово
    🍉
🍉


🏁 🍇
    🍦 аргументы 🍩🎞💻
    🍊 🍦 слово 🐽 аргументы 2 🍇
        🍦 список-эмодзи 🔷 📚📖 🔤emoji.txt🔤
        🍮 количество 0
        🔂 эмодзи 🔪 список-эмодзи слово 🍇
            😀 эмодзи
            🍫 количество
        🍉
        🍊 😛 количество 0 🍇
            😀 🍪🔤Эмодзи со словом "🔤 слово 🔤" в названиии нет🔤🍪
        🍉
    🍉
    🍓 🍇
        😀 🔤Список эмодзи с нужным словом в описании🔤
        😀 🔤Запуск: emojicode find.emojib слово🔤
    🍉
🍉
